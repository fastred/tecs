// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // zero or negate x if necessary
    Mux16(a=x, b=false, sel=zx, out=zxOut);
    Not16(in=zxOut, out=notx);
    Mux16(a=zxOut, b=notx, sel=nx, out=nxOut);

    // zero or negate y if necessary
    Mux16(a=y, b=false, sel=zy, out=zyOut);
    Not16(in=zyOut, out=noty);
    Mux16(a=zyOut, b=noty, sel=ny, out=nyOut);

    // compute result of f function
    Add16(a=nxOut, b=nyOut, out=xPlusY);
    And16(a=nxOut, b=nyOut, out=xAndY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=afterF);

    // negate if necessary and output
    Not16(in=afterF, out=negPreOut);
    Mux16(a=afterF, b=negPreOut, sel=no, out=realOut, out[15]=ng, out[0..7]=or1,
      out[8..15]=or2); // number is negative if out[15] equals 1
    Or16(a=false, b=realOut, out=out);

    // check all digits, if all are equal to zero then set 'zr' to 1
    Or8Way(in=or1, out=or1result);
    Or8Way(in=or2, out=or2result);
    Or(a=or1result, b=or2result, out=notZero);
    Not(in=notZero, out=zr);

}
