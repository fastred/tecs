// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Mux16(a=in, b=false, sel=reset, out=outReset);
    Inc16(in=outRegister, out=outAddition);

    // evaluate from right to left
    //
    // inc, load, reset, input
    // 000 a
    // 001 b
    // 010 c
    // 011 d 
    // 100 e
    // 101 f
    // 110 g
    // 111 h

    Mux8Way16(a=outRegister, b=outReset, c=in, d=outReset, e=outAddition,
      f=outReset, g=in, h=outReset, sel[0]=reset, sel[1]=load,
      sel[2]=inc, out=preOut);
    Register(in=preOut, load=true, out=out, out=outRegister);
}

