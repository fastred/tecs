// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the computer's memory,
 * including RAM and memory mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: If load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output in the next time step.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000–0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // start screen:
    // 100000000000000_2
    //
    // end screen:
    // 101111111111111_2
    //
    // keyboard:
    // 110000000000000_2

    Not(in=address[14], out=isMem);

    Or8Way(in=address[0..7], out=zerosForKeyboard1);
    Or8Way(in[0..4]=address[8..12], out=zerosForKeyboard2);
    Or(a=zerosForKeyboard1, b=zerosForKeyboard2, out=keyboardSecondPartPre);
    Not(in=keyboardSecondPartPre, out=keyboardSecondPart);
    And(a=address[13], b=address[14], out=keyboardFirstPart);
    And(a=keyboardFirstPart, b=keyboardSecondPart, out=isKeyboard);

    Not(in=address[13], out=notAdd13);
    And(a=address[14], b=notAdd13, out=isScreen);

    // is load needed for given device?
    And(a=load, b=isMem, out=loadMem);
    And(a=load, b=isScreen, out=loadScreen);

    RAM16K(in=in, load=loadMem, address=address[0..13], out=memOut);
    Screen(in=in, load=loadScreen, address=address[0..12], out=screenOut);
    Keyboard(out=keyboardOut);

    Mux8Way16(b=memOut, c=screenOut, e=keyboardOut, sel[0]=isMem,
      sel[1]=isScreen, sel[2]=isKeyboard, out=out);
}
